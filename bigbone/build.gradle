plugins {
    id 'social.bigbone.bigbone-library'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    api libs.okhttp
    api libs.gson
    implementation libs.kotlin.jdk8

    testImplementation libs.junit.jupiter.api
    testImplementation libs.kluent
    testImplementation libs.mockk
    testImplementation libs.mockk.dsl
    integrationTestImplementation libs.junit.jupiter.api
    integrationTestImplementation libs.junit.jupiter.engine
    integrationTestImplementation libs.testcontainers
    integrationTestImplementation libs.testcontainers.junit
    integrationTestImplementation 'ch.qos.logback:logback-core:1.3.5'
    integrationTestImplementation 'ch.qos.logback:logback-classic:1.3.5'
    integrationTestImplementation 'org.slf4j:slf4j-api:2.0.4'
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}
